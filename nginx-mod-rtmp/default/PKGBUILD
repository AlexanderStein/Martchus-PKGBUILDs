# $Id$
# Maintainer: Martchus <martchus@gmx.net>

pkgname=nginx-mod-rtmp
pkgver=1.2.1
pkgrel=8
_dirname="nginx-rtmp-module-$pkgver"
_nginxver=1.20.0

pkgdesc='Module for nginx that adds RTMP support'
arch=('x86_64')
depends=("nginx=$_nginxver" "libutil-linux")
makedepends=("nginx-src")
url='https://github.com/arut/nginx-rtmp-module'
license=('BSD')

source=("$pkgname-$pkgver::https://github.com/arut/nginx-rtmp-module/archive/v${pkgver}.tar.gz")
sha256sums=('87aa597400b0b5a05274ee2d23d8cb8224e12686227a0abe31d783b3a645ea37')

prepare() {
  cd "$srcdir/$_dirname"
}

build() {
  cp -r /usr/src/nginx .
  cd "$srcdir"/nginx
  ./configure --with-compat --add-dynamic-module="../$_dirname"
  make modules
}

package() {
  install -Dm755 "$srcdir/$_dirname/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  cd "$srcdir"/nginx/objs
  for mod in ngx_*.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
