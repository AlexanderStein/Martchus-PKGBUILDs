# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_pkgname=libjpeg-turbo
pkgname=static-compat-$_pkgname
pkgver=2.1.3
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline compression and decompression"
arch=(x86_64)
url="https://libjpeg-turbo.org/"
license=(BSD)
depends=(glibc-static-compat)
makedepends=(static-compat-cmake nasm)
options=(staticlibs)
source=(https://sourceforge.net/projects/$_pkgname/files/$pkgver/$_pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('975c9835de7d70c6c8c5a1959adfa18f7f956bc9946fde8f1b40c1d853245c38a3118ac06b93841ec3e90be6945c38d687d062b371496c2ee9b5638d7f60418d'
            'SKIP')
b2sums=('87ef47105e66b2ee918e95962ac59963b6adea5b49ab0db692810aee0a13a215a0ef7fe0b549ecd5151274549e12d09eb0b42439bf0a4bc1ad2c4e62dcceb77c'
        'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

build() {
  source static-compat-environment
  static-compat-cmake -DCMAKE_INSTALL_LIBDIR="$static_compat_prefix"/lib \
        -DWITH_JAVA=OFF\
        -DWITH_JPEG8=ON \
        -W no-dev \
        -B build \
        -S $_pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  source static-compat-environment
  make test -C build
}

package() {
  source static-compat-environment
  make VERBOSE=1 DESTDIR="$pkgdir" docdir="$static_compat_prefix"/share/doc/$_pkgname exampledir="$static_compat_prefix"/share/doc/$_pkgname install -C build

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s ../../doc/$_pkgname/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname"
  # header required by some dependants
  # https://bugs.archlinux.org/task/24787
  install -vDm 644 $_pkgname-$pkgver/jpegint.h "$pkgdir/$static_compat_prefix/include"
  rm -r "$pkgdir/$static_compat_prefix"/share
  find "$pkgdir/$static_compat_prefix"/lib -iname '*.so*' -delete
}
