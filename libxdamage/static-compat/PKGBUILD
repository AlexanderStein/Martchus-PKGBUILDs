# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=libxdamage
pkgname=static-compat-$_pkgname
pkgver=1.1.5
pkgrel=3
pkgdesc="X11 damaged region extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('static-compat-libxfixes')
makedepends=('static-compat-xorgproto' 'static-compat-configure')
options=(staticlibs !emptydirs)
source=(${url}/releases/individual/lib/libXdamage-${pkgver}.tar.bz2{,.sig})
sha512sums=('a3ca6cc33b1727f717a3e2aa5593f660508a81a47918a0aa949e9e8fba105e303fe5071983b48caac92feea0fe6e8e01620805e4d19b41f21f20d837b191c124'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  source static-compat-environment
  cd libXdamage-${pkgver}
  static-compat-configure --disable-shared --enable-static
  make
}

package() {
  source static-compat-environment
  cd libXdamage-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
