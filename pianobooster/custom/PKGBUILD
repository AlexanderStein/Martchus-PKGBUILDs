_pkgname='PianoBooster'
pkgname='pianobooster-custom'
pkgdesc='A MIDI file player that teaches you how to play the piano (with custom, experimental patches)'
pkgver=v1.0.0.r20.gaaafb07
pkgrel=1
depends=('fluidsynth' 'ftgl' 'hicolor-icon-theme' 'qt5-base' 'rtmidi')
makedepends=('git' 'cmake' 'qt5-tools' 'ninja')
optdepends=('qt5-translations')
conflicts=('pianobooster')
arch=('x86_64')
url='https://www.pianobooster.org'
license=('GPL3')
source=('git+https://github.com/Martchus/PianoBooster#branch=custom')
sha256sums=(SKIP)

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build () {
  cd "${srcdir}/${_pkgname}"
  cmake -G Ninja -B build \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_SYSTEM_FONT=ON \
        -DUSE_JACK=ON \
        -DWITH_MAN=ON \
        .
  VERBOSE=1 cmake --build build
}

package () {
  cd "${srcdir}/${_pkgname}"
  DESTDIR="$pkgdir" cmake --install build
}
