# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=glib2
pkgname=static-compat-$_pkgname
pkgver=2.74.0
pkgrel=1
pkgdesc="Low level core library"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(static-compat-pcre2 static-compat-libffi static-compat-util-linux static-compat-zlib)
makedepends=(gettext shared-mime-info python libelf git static-compat-meson)
checkdepends=(desktop-file-utils)
options=(!strip !emptydirs staticlibs)
_commit=30bd57ecf8aa051de9848ba5a2b140f4810401ff  # tags/2.72.3^0
source=("git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
        0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch)
sha256sums=('SKIP'
            '6d51eb5856268d79eee01b97a299fa9f99db18b2abb4df56f2ed9e641a09138a')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib

  # Suppress noise from glib-compile-schemas.hook
  git apply -3 ../0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch

  # https://bugs.archlinux.org/task/75980
  # https://gitlab.gnome.org/GNOME/glib/-/merge_requests/2921
  git cherry-pick -n 'ea3f17d598d550345e94e4571130e429443e91cb'
}

build() {
  export CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  export LDFLAGS+=" -lblkid"
  source static-compat-environment
  static-compat-meson glib build \
    -D selinux=disabled \
    -Dsysprof=disabled \
    -D man=false \
    -D gtk_doc=false
  meson compile -C build
}

check() {
  source static-compat-environment
  meson test -C build --no-suite flaky --print-errorlogs
}

package() {
  source static-compat-environment
  DESTDIR="$pkgdir" meson install -C build

  # Strip messes up the LTO-enabled archive's index so strip libs manually and then run ranlib again
  find "$pkgdir" -name '*.a' -exec strip $STRIP_STATIC {} \;
  find "$pkgdir" -name '*.a' -exec ranlib {} \;

  export PYTHONHASHSEED=0
  python -m compileall -d "$static_compat_prefix"/share/glib-2.0/codegen \
    "$pkgdir/$static_compat_prefix/share/glib-2.0/codegen"
  python -O -m compileall -d "$static_compat_prefix"/share/glib-2.0/codegen \
    "$pkgdir/$static_compat_prefix/share/glib-2.0/codegen"
}

# vim:set sw=2 et:
